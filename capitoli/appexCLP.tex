%Andrea Borghesi
%Università degli studi di Bologna

% appendice sulla programmazione logica a vincoli

\clearpage{\pagestyle{empty}\cleardoublepage}
\chapter{Esempi di CLP} 
\label{appendiceCLP} 

Illustreremo ora due esempi di modellazione di problemi a vincoli sfruttando il linguaggio ECL$^i$PS$^e$ (nel primo caso considerando domini finiti e nel secondo valori reali, avvalendoci anche della libreria Eplex), per mostrare come possono essere strutturati i problemi di programmazione logica a vincoli; in questa trattazione supporremo noti i concetti elementari della programmazione logica (procedimenti risolutivi, definizioni di un termine, etc.), la cui discussione esula da questo lavoro.

\myparagraph{Esempio \clpfd}
Il cosiddetto \emph{Send More Money} puzzle è un esempio classico di programmazione a vincoli; le variabili $[S,E,N,D,M,O,R,Y]$ rappresentano cifre da 0 a 9 e lo scopo è assegnare alle variabili valori diversi in modo che l'operazione aritmetica di Figura~\ref{SendMoreMoney} risulti corretta - inoltre i numeri devono essere ben formati, da cui $S>0$ e $M>0$. 

\begin{figure}[h]
	\centering
	\includegraphics[width=0.215\textwidth]{sendMoreMoney}
	\caption{Send More Money puzzle}
	\label{SendMoreMoney}
\end{figure}

Con la programmazione convenzionale si avrebbe necessità di esprimere una strategia di ricerca in modo esplicito (senza contare possibili ottimizzazioni come cicli innestati), mentre con linguaggi logici come Prolog verrebbe sfruttata la ricerca fornita dal risolutore interno (il motore inferenziale), con il vantaggio di una programmazione estremamente facilitata ma col rischio di un'efficienza non elevata - a meno di programmi ottimizzati, i quali richiederebbero comunque maggiori tempo e abilità. 

Questo è in effetti il campo di applicazione ideale della programmazione logica a vincoli, in particolare nell'ambito  dei domini finiti \clpfd: le variabili possono assumere valori appartenenti ad un insieme finito di numeri interi, i vincoli sono facilmente esprimibili formalmente e occorre effettuare una certa quantità di ricerca nello spazio delle soluzioni. In questo problema sarebbe naturale usare le variabili del programma per rappresentare le diverse cifre e la soluzione finale dovrà essere un assegnamento di un valore unico per ogni variabile. 

Risolvere questo problema con Prolog comporta l'utilizzo della strategia di ricerca chiamata \emph{Generate and Test}, che prevede che prima la generazione di una soluzione e poi la verifica della consistenza dei vincoli e, nel caso che questa dia esito negativo, l'assegnamento di nuovi valori alle variabili seguita da nuova verifica e così via. In questo modo l'esplorazione dello spazio delle soluzioni è chiaramente inefficiente - per esempio la possibile implementazione in Prolog mostrata qui sotto, per quanto suscettibile a miglioramenti, deve gestire $\frac{10!}{2}$ possibili assegnamenti di valori alle variabili. 

\lstset{language=Prolog}
\begin{lstlisting}
% Send More Money puzzle in Prolog
smm :-
        X = [S,E,N,D,M,O,R,Y],           % variabili
        Digits = [0,1,2,3,4,5,6,7,8,9],	 % domini
        
        % predicato che assegna una soluzione
        assign_digits(X, Digits),
       	
       	%  verifica dei vincoli vincoli
        M > 0, 
        S > 0,
                  1000*S + 100*E + 10*N + D +
                  1000*M + 100*O + 10*R + E =:=
        10000*M + 1000*O + 100*N + 10*E + Y,
        write(X).

select(X, [X|R], R).
select(X, [Y|Xs], [Y|Ys]):- select(X, Xs, Ys).

assign_digits([], _List).
assign_digits([D|Ds], List):-
        select(D, List, NewList),
        assign_digits(Ds, NewList).
\end{lstlisting}

L'implementazione realizzata con ECL$^i$PS$^e$ presenta i vantaggi di semplificare ulteriormente la modellazione del problema e di appoggiarsi all'efficiente risolutore interno per l'esplorazione dello spazio delle soluzioni, in modo particolare il fatto che ogni volta che una variabile viene istanziata i vincoli vengono propagati per eliminare a priori strade inconsistenti, riducendo gli spazi delle soluzioni e prevenendo fallimenti sicuri. 

\begin{lstlisting}
% Send More Money puzzle in ECLiPSe
smm :-
     X = [S,E,N,D,M,O,R,Y],		% variabili
     X :: [0 .. 9],				% domini finiti
     
     % vincoli
     M #> 0,
     S #> 0,
               1000*S + 100*E + 10*N + D +
               1000*M + 100*O + 10*R + E #=
     10000*M + 1000*O + 100*N + 10*E + Y,
     alldistinct(X),
     
     % ricerca della soluzione
     labeling(X),
     write(X).

\end{lstlisting}

\myparagraph{Esempio \clpr - Eplex}

Presentiamo ora un esempio di un problema (Fig.~\ref{clpR_example}) che rientra nell'ambito dei \clpr e che fa uso della libreria Eplex, tratto dal manuale di ECL$^i$PS$^e$ \cite{eclipseTut}. Ci sono tre impianti, o fabbriche, (1-3) in grado di produrre un certo prodotto con capacità diverse e i cui prodotti devono essere trasportati a quattro clienti (A-D) con quantità richieste diverse; anche il costo unitario di trasporto ai clienti è variabile. L'obiettivo del problema è minimizzare i costi di trasporto soddisfacendo le esigenze dei clienti. 

\begin{figure}[h]
	\centering
	\includegraphics[scale=0.6]{clpR_example}
	\caption{Esempio di un problema \clpr. Fonte \cite{eclipseTut}}
	\label{clpR_example}
\end{figure}

Per formulare il problema definiamo la quantità di prodotto trasportata dall'impianto $N$ al cliente $p$ come variabile $N_p$ - ad esempio $A_1$ rappresenta il costo di trasporto dalla fabbrica $A$ al cliente $1$. I vincoli da considerare sono di due tipi (sempre facendo riferimento alla Figura~\ref{clpR_example}):
\begin{itemize}
\item La quantità di prodotto consegnata da tutti gli impianti a un cliente deve essere uguale alla domanda del cliente, ad esempio per il cliente $A$ che può essere rifornito dagli impianti 1-3, abbiamo che $A_1+A_2+A_3=21$
\item La quantità di prodotto in uscita da una fabbrica non può essere superiore alla sua capacità produttiva, ad esempio per l'impianto $1$ che invia prodotti ai clienti A-D si ha che $A_1+B_1+C_1+D_1 \leq 50$
\end{itemize}   
Poiché lo scopo è minimizzare i costi di trasporto, la funzione obiettivo è di minimizzare i costi combinati del trasporto dei prodotti dai tre impianti a tutti e quattro i clienti.

La formulazione del problema è quindi la seguente.\\*
Funzione obiettivo: 
\begin{equation}  \label{exampleClpRObiett}
	\min(10A_1+7A_2+200A_3+8B_1+5B_2+10B_3+5C_2+5C_2+8C_3+9D_1+3D_2+7D_3)
\end{equation}
Vincoli:
\begin{equation}  \label{exampleCons1}
	A_1+A_2+A_3=21
\end{equation}
\begin{equation}  \label{exampleCons2}
	B_1+B_2+B_3=40
\end{equation}
\begin{equation}  \label{exampleCons3}
	C_1+C_2+C_3=34
\end{equation}
\begin{equation}  \label{exampleCons4}
	D_1+D_2+D_3=10
\end{equation}
\begin{equation}  \label{exampleCons5}
	A_1+B_1+C_1+D_1 \leq 50
\end{equation}
\begin{equation}  \label{exampleCons6}
	A_2+B_2+C_2+D_2 \leq 30
\end{equation}
\begin{equation}  \label{exampleCons7}
	A_3+B_3+C_3+D_3 \leq 40
\end{equation}

Mostriamo ora come questo problema venga modellato sfruttando la libreria Eplex. In primo luogo occorre caricare la libreria Eplex di cui si dispone (in questo caso abbiamo sfruttato un risolutore esterno open source) e ottenerne un'\emph{istanza}, la quale rappresenta un singolo problema sotto forma di modulo, a cui possono essere riferiti vincoli e funzione obiettivo consentendo quindi al solver esterno di risolvere il problema. Il codice che segue mostra come il problema di Figura~\ref{clpR_example} sia stato trasposto all'interno di ECL$^i$PS$^e$.

\begin{lstlisting}
:- lib(eplex).		% caricamento della libreria Eplex
:- eplex_instance(prob).		% definizione dell'istanza - chiamata 'prob'

main(Cost, Vars) :-
		% dichiarazione delle variabili e definizione del loro dominio		
		Vars = [A1,A2,A3,B1,B2,B3,C1,C2,C3,D1,D2,D3],
		prob: (Vars $:: 0.0..1.0Inf),  % valori maggiori o uguali a 0
		
		% definizione dei vincoli applicati all'istanza eplex
		prob: (A1 + A2 + A3 $= 21), 
		prob: (B1 + B2 + B3 $= 40),
		prob: (C1 + C2 + C3 $= 34),
		prob: (D1 + D2 + D3 $= 10),

		prob: (A1 + B1 + C1 + D1 $=< 50),
		prob: (A2 + B2 + C2 + D2 $=< 30),
		prob: (A3 + B3 + C3 + D3 $=< 40),

		% inizializza il solver esterno con la funzione obiettivo
		prob: eplex_solver_setup(min(10*A1 + 7*A2 + 200*A3 + 
			8*B1 + 5*B2 + 10*B3 +
		 	5*C1 + 5*C2 + 8*C3 +
		 	9*D1 + 3*D2 + 7*D3)),

		% ---------- Fine Modellazione ----------

		% risoluzione del problema
		prob: eplex_solve(Cost).
\end{lstlisting}

Per usare un'istanza Eplex occorre prima dichiararla con \emph{eplex\_instance/1}; una volta dichiarata, l'istanza viene riferita tramite il nome specificato. 

Come primo passo creiamo le variabili del problema e imponiamo che possano assumere solamente valori non negativi e rendiamo noti all'istanza il loro dominio (\emph{\$::/2}). Successivamente imponiamo i vincoli che modellano il problema sotto forma di uguaglianze e disuguaglianze aritmetiche; per via del solver esterno scelto, gli unici tipi di vincoli accettati sono quelli lineari - che ovviamente consentono una maggiore efficienza nella risoluzione.

Occorre poi inizializzare il risolutore esterno con l'istanza eplex creata, in modo che questa possa essere risolta. Questo è fatto dal \emph{eplex\_solver\_setup/1}, che prende come argomento la funzione obiettivo, la quale può essere di minimizzazione o massimizzazione. Infine è possibile risolvere il problema modellato attraverso \emph{eplex\_solve/1}.

Quando un'istanza viene risolta, il solver prende in considerazione tutti i vincoli ad essa relativi, i valori che le variabili del problema possono assumere e la funzione obiettivo specificata. In questo caso è possibile ottenere una soluzione ottimale pari a 710.0: 
\begin{lstlisting}
?-	main(Cost, Vars).

Cost = 710.0
Vars = [A1{0.0 .. 1e+20 @ 0.0}, A2{0.0 .. 1e+20 @ 21.0}, ....]
\end{lstlisting}
